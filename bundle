#!/usr/bin/env zsh
#
# Created by M. Massenzio, 2022-01-21
#
# Usage: bundle [--manifest MANIFEST] SRC [OUT]
#
# Creates the policy bundle in the OUT directory, from the Rego files in SRC.
#
#    SRC          the folder containing all the Rego files. Required.
#    OUT          where the bundle (.tar.gz) will be placed (and read from the OPA server)
#                 If unspecified, out/bundles/
#    MANIFEST     the OPA bundle Manifest (JSON); if unspecified we will use SRC/manifest.json"
#
# Requires the common-utils utilities (https://github.com/massenz/common-utils)
set -eu
source ${UTILS_DIR}/utils

PARSED=$(parse-args  manifest SRC+ "dest?" -- $@)
source ${PARSED}

if [[ -z ${SRC} || ! -d ${SRC} ]]
then
  fatal "SRC must be specified and be a valid directory"
fi

declare -r DEST=${dest:-"out/bundles"}
declare -r MANIFEST=${manifest:-${SRC}/manifest.json}
if [[ ! -f ${MANIFEST} ]]
then
  fatal "A valid Manifest must be specified: ${MANIFEST} not found"
fi
msg "Building bundle from ${SRC} Rego sources, to ${DEST}"
declare -r REV=$(cat ${MANIFEST} | jq -r ".revision")
declare -r ROOT=$(cat ${MANIFEST} | jq -r ".roots[0]")
declare -r TMP=$(mktemp -d)

declare -r ARC="authz-${REV}.tar.gz"
msg "Bundle: ${ARC}"

mkdir -p ${DEST}
if [[ -e ${DEST}/${ARC} ]];
then
  read -q "?Bundle ${DEST}/${ARC} exists. Overwrite [y/n]? "
  echo ""
fi
mkdir -p ${TMP}/${ROOT}
cp -r ${SRC}/*.rego ${TMP}/${ROOT}
cp ${MANIFEST} ${TMP}/.manifest

# Needed to deal with MacOS "quarantine" nonsense.
# See: https://apple.stackexchange.com/questions/75989/why-does-osx-add-extra-filename-when-i-tar-a-directory
export COPYFILE_DISABLE=1
tar --create --file ${DEST}/${ARC} -z -C $TMP .manifest ${ROOT}
success "Bundle (Rev. ${REV}) created. Use -bundle ${DEST}/${ARC}"
